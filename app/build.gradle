
plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
}

apply plugin: 'kotlin-parcelize'

android {
    namespace 'com.fam4k007.videoplayer'
    compileSdkVersion 34
    
    // ========== APK 分包配置 ==========
    // 只编译 64 位架构，加快编译速度
    splits {
        abi {
            enable true
            reset()
            include 'arm64-v8a'  // 只包含 64 位架构
            universalApk false  // 不生成包含所有架构的通用 APK
        }
    }
    
    defaultConfig {
        applicationId "com.fam4k007.videoplayer"
        minSdkVersion 26
        targetSdkVersion 34
        versionCode 11
        versionName "1.1.1"
        
        // 移除 defaultConfig 中的 ndk 配置，让各个 buildType 自己控制
    }
    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
            
            // Debug 版本也使用分包配置，由 splits 控制
        }
        
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            
            // Release 版本也使用分包配置，由 splits 控制
        }
    }
    
    compileOptions {
        sourceCompatibility = 17
        targetCompatibility = 17
    }
    kotlinOptions {
        jvmTarget = "17"
    }
    
    buildFeatures {
        viewBinding true
        compose true
    }
    
    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.3"  // 兼容 Kotlin 1.9.10
    }
    
    // ========== 为分包 APK 设置不同的版本号 ==========
    // 这样 Google Play 可以为不同设备提供正确的 APK
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def abiName = output.getFilter(com.android.build.OutputFile.ABI)
            if (abiName != null) {
                // 为不同架构分配不同的版本码偏移
                // arm64-v8a: +2, armeabi-v7a: +1
                def abiVersionCodes = [
                    'arm64-v8a': 2,
                    'armeabi-v7a': 1
                ]
                output.versionCodeOverride = 
                    defaultConfig.versionCode * 10 + (abiVersionCodes[abiName] ?: 0)
            }
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.9.10"
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.recyclerview:recyclerview:1.3.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    
    // Compose BOM (Bill of Materials)
    def composeBom = platform('androidx.compose:compose-bom:2023.10.01')
    implementation composeBom
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.activity:activity-compose:1.8.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.6.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-compose:2.6.1'
    implementation 'androidx.compose.material:material-icons-extended'  // Material Icons Extended
    debugImplementation 'androidx.compose.ui:ui-tooling'
    
    implementation(name: 'mpv-android-lib-v0.1.10', ext: 'aar')
    // Danmaku - 弹幕库
    implementation(name: 'DanmakuFlameMaster', ext: 'aar')
    // WebDAV - Sardine library
    implementation files('libs/sardine-1.0.2.jar')
    implementation files('libs/simple-xml-2.7.1.jar')
    // Glide - image loading for video thumbnails
    implementation 'com.github.bumptech.glide:glide:4.15.1'
    // Lifecycle and Coroutines
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'
    // OkHttp - for network requests
    implementation 'com.squareup.okhttp3:okhttp:4.11.0'
    // Jsoup - for HTML parsing
    implementation 'org.jsoup:jsoup:1.16.1'
    // Room Database
    implementation 'androidx.room:room-runtime:2.5.2'
    implementation 'androidx.room:room-ktx:2.5.2'
    kapt 'androidx.room:room-compiler:2.5.2'
    
    // 二维码生成 - B站扫码登录
    implementation 'com.google.zxing:core:3.5.1'
    
    // 图片加载 (Coil) - B站番剧封面
    implementation 'io.coil-kt:coil-compose:2.5.0'
    implementation 'io.coil-kt:coil-video:2.5.0'  // 视频帧提取支持
    
    // Gson - JSON解析
    implementation 'com.google.code.gson:gson:2.10.1'
    
    // 加密存储 - EncryptedSharedPreferences (AndroidX Security)
    implementation 'androidx.security:security-crypto:1.1.0-alpha06'
    
    // If you use annotation processing for Glide's generated API, enable kapt and add the compiler below:
    // kapt 'com.github.bumptech.glide:compiler:4.15.1'
}
